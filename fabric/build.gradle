plugins {
    id 'multiloader-loader'
    id 'fabric-loom' version '1.8-SNAPSHOT'
}

repositories {
    exclusiveContent {
        forRepository {
            maven {
                name = "TerraformersMC"
                url = "https://maven.terraformersmc.com/"
            }
        }
        filter {
            includeGroup("com.terraformersmc")
        }
    }
}

fabricApi {
    configureDataGeneration {
        outputDirectory.set(file("src/generated"))
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    modImplementation "com.mrcrayfish:framework-fabric:${minecraft_version}-${framework_version}"
    modRuntimeOnly "com.mrcrayfish:catalogue-fabric:${minecraft_version}-${catalogue_version}"
    modRuntimeOnly "com.terraformersmc:modmenu:${modmenu_version}"
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

loom {
    def aw = project(":common").file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) {
        accessWidenerPath.set(aw)
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
}
